import io.gitlab.arturbosch.detekt.Detekt
import io.gitlab.arturbosch.detekt.DetektPlugin
import io.gitlab.arturbosch.detekt.report.ReportMergeTask

buildscript {
    apply from: "$rootDir/team-props/git-hooks/git-hooks.gradle.kts"
    dependencies {
        classpath 'com.google.gms:google-services:4.3.15'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    }
}

plugins {
    id 'com.android.application' version '7.4.1' apply false
    id 'com.android.library' version '7.4.1' apply false
    id 'org.jetbrains.kotlin.android' version '1.8.10' apply false
    id 'io.gitlab.arturbosch.detekt' version '1.22.0' apply false
    id 'nl.neotech.plugin.rootcoverage' version '1.6.0'
}

tasks.register("clean", Delete) {
    delete(rootProject.buildDir)
}

tasks.register("reportMerge", ReportMergeTask) {
    output = project.layout.buildDirectory.file("reports/detekt/merge.xml")
}

rootCoverage {
    buildVariant "debug"
    excludes = [
            "**/bumptech/glide/*.*",
            "androidx/**/*.class",
            "**/*Test*.*",
            "**/*Application*.*",
            "**/*Activity*.*",
            "**/*Fragment*.*",
            "**/*Adapter*.*",
            "**/*Dialog*.*",
            "**/*Args*.*",
            "**/*Companion*.*",
            "**/*Binder*.*",
            "**/auth/sample/**/*"
    ]
    generateCsv false
    generateHtml true
    generateXml false
    executeAndroidTests false
    executeUnitTests true
    includeAndroidTestResults false
    includeUnitTestResults true
}

allprojects {
    apply from: "$rootDir/team-props/formatting/ktlint.gradle"
    apply from: "$rootDir/ext.gradle"
    apply plugin: "io.gitlab.arturbosch.detekt"

    tasks.withType(Detekt).configureEach {
        config.setFrom("$rootDir/team-props/lint/detekt/detekt.yml")
        jvmTarget = "1.8"
        reports {
            xml.required.set(true)
            html.required.set(true)
        }
    }

    plugins.withType(DetektPlugin) {
        tasks.withType(Detekt) { detektTask ->
            finalizedBy(reportMerge)

            reportMerge.configure { mergeTask ->
                mergeTask.input.from(detektTask.xmlReportFile)
            }
        }
    }
}


